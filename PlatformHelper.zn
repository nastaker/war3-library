library PlatformHelper requires Commons, SystemBinary, YDWERPGBilling, YDWERPGScores {

  dialog dlgTitle;
  integer TITLE_COUNT = 10;
  button btnTitle[];
  integer IssuedSource[];

  function SetPlayerTitle(player p, integer titleIndex) {
    integer playerId = GetPlayerId(p);
    if (titleIndex <= 0) {
      return;
    }
    if (!havebinary(PlayerTitles[playerId], PlayerTitleCurr[playerId]-1)) {
      PlayerTitles[playerId] = PlayerTitles[playerId] + R2I(Pow(2, PlayerTitleCurr[playerId]-1));
      YDWERPGAddKey(p, "chjf", R2I(Pow(2, PlayerTitleCurr[playerId]-1)));
    }
    PlayerTitleCurr[playerId] = titleIndex;
    YDWERPGSetKey(p, "ch", titleIndex);
    if (!havebinary(PlayerTitles[playerId], titleIndex-1)) {
      PlayerTitles[playerId] = PlayerTitles[playerId] + R2I(Pow(2, titleIndex-1));
      YDWERPGAddKey(p, "chjf", R2I(Pow(2, titleIndex-1)));
    }
  }

  function DistributePlayerResourceAction() {
    // 发放积分资源，每轮最多发放4000*轮数资源
    integer playerId = GetPlayerId(GetEnumPlayer());
    integer source = ISSUE_SOURCE_AMOUNT * (1+udg_RoundIndex);
    integer sourceGold = 0;
    integer sourceLumber = 0;
    if (IssuedSource[playerId] == 0) {
      return;
    }
    // 检查IssuedSource资源是否足够，若足够IssuedSource减去source；若IssuedSource不足，则source改为IssuedSource，并将IssuedSource清零
    if (IssuedSource[playerId] > source) {
      IssuedSource[playerId] = IssuedSource[playerId] - source;
    } else {
      source = IssuedSource[playerId];
      IssuedSource[playerId] = 0;
    }
    // 按3：1比例发放
    sourceGold = R2I(source * 3 * 0.25);
    sourceLumber = R2I(source * 1 * 0.25);
    
    SetPlayerState(Player(playerId), PLAYER_STATE_RESOURCE_GOLD, GetPlayerState(Player(playerId), PLAYER_STATE_RESOURCE_GOLD) + sourceGold);
    SetPlayerState(Player(playerId), PLAYER_STATE_RESOURCE_LUMBER, GetPlayerState(Player(playerId), PLAYER_STATE_RESOURCE_LUMBER) + sourceLumber);
    DisplayTextToPlayer(Player(playerId), 0, 0, "|cff66ccff获得资源："+I2S(sourceGold)+"金币，"+I2S(sourceLumber)+"木材。（由积分转化）|r");
  }

  public {
    boolean isPlayerChangedTitle[];
    integer PlayerWinStreak[];
    integer PlayerWinCount[];
    integer PlayerGameCount[];
    integer PlayerScore[];
    integer PlayerGeneralLevel[];
    integer PlayerUnitKills[];
    integer PlayerTitles[];
    integer PlayerTitleCurr[];

    integer addedScore[];

    function DialogTitleClick() {
      integer i;
      for (0 <= i < TITLE_COUNT) {
        if (GetClickedButton() == btnTitle[i]) {
          SetPlayerTitle(GetTriggerPlayer(), i+1);
          isPlayerChangedTitle[GetPlayerId(GetTriggerPlayer())] = true;
          DisplayTextToPlayer(GetTriggerPlayer(), 0, 0, "已更换称号为："+PlayerTitleStr[i+1]+"。");
          break;
        }
      }
    }

    function InitDialogs() {
      trigger trigDialog = CreateTrigger();
      dlgTitle = DialogCreate();
      // 难度选择对话框被点击事件;
      TriggerRegisterDialogEvent(trigDialog, dlgTitle);
      TriggerAddAction(trigDialog, function DialogTitleClick);
      trigDialog = null;
    }

    function ShowTitleChangeDialog(player p) {
      integer i;
      integer playerId = GetPlayerId(p);
      if (isPlayerChangedTitle[playerId]) {
        DisplayTextToPlayer(p, 0, 0, "你本局已更换过称号，无法再更换。");
        return;
      }
      DialogClear(dlgTitle);
      DialogSetMessage(dlgTitle, "请选择称号");
      for (0 <= i <= TITLE_COUNT) {
        if (havebinary(PlayerTitles[playerId], i) && i < TITLE_COUNT) {
          if (i == 8) {
            btnTitle[i] = DialogAddButton(dlgTitle, GeneralTitleStr[PlayerGeneralLevel[playerId]], 0);
          } else {
            btnTitle[i] = DialogAddButton(dlgTitle, PlayerTitleStr[i+1], 0);
          }
        } else if (i==TITLE_COUNT) {
          btnTitle[i] = DialogAddButton(dlgTitle, "取消", 0);
        }
      }
      DialogDisplay(p, dlgTitle, true);
      p = null;
    }

    function UnitAddWing(unit u) {
      integer playerId = GetPlayerId(GetOwningPlayer(u));
      if (!IsUnitType(u, UNIT_TYPE_HERO)) {
        u = null;
        return;
      }
      if (udg_PlayerTitleApply[playerId] == 5) {
        UnitAddAbility(u, WINGIDS[5]);
        UnitMakeAbilityPermanent(u, true, WINGIDS[5]);
      } else if (havebinary(PlayerTitles[playerId], 2)) {
        if (GetUnitRace(u) == RACE_HUMAN) {
          UnitAddAbility(u, WINGIDS[0]);
          UnitMakeAbilityPermanent(u, true, WINGIDS[0]);
        } else if (GetUnitRace(u) == RACE_ORC) {
          UnitAddAbility(u, WINGIDS[1]);
          UnitMakeAbilityPermanent(u, true, WINGIDS[1]);
        } else if (GetUnitRace(u) == RACE_UNDEAD) {
          UnitAddAbility(u, WINGIDS[2]);
          UnitMakeAbilityPermanent(u, true, WINGIDS[2]);
        } else if (GetUnitRace(u) == RACE_NIGHTELF) {
          UnitAddAbility(u, WINGIDS[3]);
          UnitMakeAbilityPermanent(u, true, WINGIDS[3]);
        } else {
          UnitAddAbility(u, WINGIDS[4]);
          UnitMakeAbilityPermanent(u, true, WINGIDS[4]);
        }
      }
      u = null;
    }

    function InitPlatQuest() {
      CreateQuestBJ(bj_QUESTTYPE_OPT_DISCOVERED, "一骑当千", "获得方法：击杀？？？个以上敌人。\n效果：仅佩戴时，英雄升级时获得额外的全属性、攻击速度、防御力及每秒恢复的生命值。", "ReplaceableTextures\\CommandButtons\\BTNStaffOfPreservation.blp");
      CreateQuestBJ(bj_QUESTTYPE_OPT_DISCOVERED, "将军", "获得方法：使用单位击杀？？？个以上敌人。\n效果：仅佩戴时，根据将军职位，提升单位升级效果。\n使用-cx查询。", "ReplaceableTextures\\CommandButtons\\BTNStaffOfPreservation.blp");
      CreateQuestBJ(bj_QUESTTYPE_OPT_DISCOVERED, "建筑师", "获得方法：建造完成超过？？？个建筑。\n效果：仅佩戴时，建造及建筑升级速度提升", "ReplaceableTextures\\CommandButtons\\BTNStaffOfPreservation.blp");
    }

    // 查看玩家是否在会员列表中，若存在，则写入对应的list中，以便后面第二次直接查询。type=1为金币列表，type=2为木材列表，type=4为小翅膀列表，type=8为大翅膀列表，type=16为氪金翅膀列表
    function FindNameInList(string str, integer playerId, integer t) -> boolean {
      integer index = 0;
      integer i = 0;
      string tmp;
      string char;
      boolean isIn = false;

      for (0 <= i < StringLength(str)) {
        char = SubString(str, i, i+1);
        if (char == "/") {
          // 
          tmp = SubString(str, index, i+1);
          if (tmp == "/" + GetPlayerName(Player(playerId)) + "/")  {
            isIn = true;
            // t > 0 才写入，否则仅查询，暂时不写入。
            // if (t > 0) {
            //   // 说明有这个ID,根据t(type的缩写，type是关键字不能用)的值，添加进对应的数组中
            //   if (havebinary(t, 0)) {
            //     PlayerGoldList[playerId] = true;
            //   }
            //   if (havebinary(t, 1)) {
            //     PlayerLumberList[playerId] = true;
            //   }
            //   if (havebinary(t, 2)) {
            //     PlayerLittleWingList[playerId] = true;
            //   }
            //   if (havebinary(t, 3)) {
            //     PlayerBigWingList[playerId] = true;
            //   }
            //   if (havebinary(t, 4)) {
            //     PlayerAngelWingList[playerId] = true;
            //   }
            // }
            break;
          }
          index = i;
        }
      }

      return isIn;
    }

    function PlayerIsInGoldList(integer playerId) -> boolean {
      return FindNameInList(GOLD_LIST, playerId, 0);
    }

    function PlayerIsInLumberList(integer playerId) -> boolean {
      return FindNameInList(LUMBER_LIST, playerId, 0);
    }

    function InitPlayerPlatScore() {
      integer i = 0;
      integer gold = 0;
      integer lumber = 0;
      for (0 <= i < 8) {
        if (GetPlayerController(Player(i))==MAP_CONTROL_USER && GetPlayerSlotState(Player(i)) == PLAYER_SLOT_STATE_PLAYING) {
          udg_PlayerGeneralApply[i] = 1.0;
          udg_HeroDamageRatio[i] = UltimateDamageRatio;

          PlayerTitles[i] = YDWERPGGetKey(Player(i), "chjf");
          PlayerTitleCurr[i] = YDWERPGGetKey(Player(i), "ch");
          // 玩家将军等级
          PlayerGeneralLevel[i] = YDWERPGGetKey(Player(i), "jj");
          // 玩家单位杀敌数
          PlayerUnitKills[i] = YDWERPGGetKey(Player(i), "dwsd");
          udg_UnitKills[i] = PlayerUnitKills[i];
          PlayerScore[i] = YDWERPGGetKey(Player(i), "jf");
          // 已发放资源，初始化为0
          IssuedSource[i] = PlayerScore[i];
          PlayerWinStreak[i] = YDWERPGGetKey(Player(i), "ws"+I2S(udg_CurrDiff+1));
          PlayerWinCount[i] = YDWERPGGetKey(Player(i), "wc"+I2S(udg_CurrDiff+1));
          udg_PlayerTitleApply[i] = PlayerTitleCurr[i];
          // 默认设置连胜数清零，结束后若胜利则+1
          YDWERPGSetKey(Player(i), "ws"+I2S(udg_CurrDiff+1), 0);
          // 若玩家单位击杀等于0，则将积分*0.03后转为击杀数量
          if (udg_UnitKills[i] == 0) {
            udg_UnitKills[i] = R2I(PlayerScore[i] * 0.03);
          }
          gold = GetPlayerState(Player(i), PLAYER_STATE_RESOURCE_GOLD);
          lumber = GetPlayerState(Player(i), PLAYER_STATE_RESOURCE_LUMBER);
          //
          if (YDWERPGBillingHasStatus(Player(i), "20010779") || PlayerIsInGoldList(i)) {
            gold = gold + ITEM_GOLD_AMOUNT;
            DisplayTimedTextToPlayer(Player(i), 0, 0, 30.0, YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "|cffffcc00购买了金币，初始资源增加"+I2S(ITEM_GOLD_AMOUNT)+"金币。|r");
          }
          if (YDWERPGBillingHasStatus(Player(i), "20010780") || PlayerIsInLumberList(i)) {
            lumber = lumber + ITEM_LUMBER_AMOUNT;
            DisplayTimedTextToPlayer(Player(i), 0, 0, 30.0, YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "|cffffcc00购买了木材，初始资源增加"+I2S(ITEM_LUMBER_AMOUNT)+"木材。|r");
          }
          if (udg_PlayerTitleApply[i] == 1) {
            udg_PlayerGeneralApply[i] = 1.0 + PlayerGeneralLevel[i] * GENERAL_INCREASE_RATIO;
            DisplayTimedTextToPlayer(Player(i), 0, 0, 30.0, YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "当前称号"+PlayerTitleStr[udg_PlayerTitleApply[i]]+"：|cffffcc00提升所带领部队士气。|r");
          } else if (udg_PlayerTitleApply[i] == 2) {
            udg_HeroDamageRatio[i] = udg_HeroDamageRatio[i] + 0.5;
            DisplayTimedTextToPlayer(Player(i), 0, 0, 30.0, YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "当前称号"+PlayerTitleStr[udg_PlayerTitleApply[i]]+"：|cffffcc00英雄等级提升时获得额外的属性点、攻击速度、防御力、每秒生命恢复速度。|r");
          } else if (udg_PlayerTitleApply[i] == 3) {
            udg_PlayerBuildSpeed[i] = 80;
            DisplayTimedTextToPlayer(Player(i), 0, 0, 30.0, YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "当前称号"+PlayerTitleStr[udg_PlayerTitleApply[i]]+"：|cffffcc00建造速度提升。|r");
          } 
          SetPlayerState(Player(i), PLAYER_STATE_RESOURCE_GOLD, gold);
          SetPlayerState(Player(i), PLAYER_STATE_RESOURCE_LUMBER, lumber);
        }
      }
    }

    function AddPlayerPlatScore(player p, integer score) {
      integer playerId = GetPlayerId(p);
      if (addedScore[playerId] + score > MaxJF) {
        addedScore[playerId] = MaxJF;
        score = MaxJF - addedScore[playerId];
      } else {
        addedScore[playerId] = addedScore[playerId] + score;
      }
      if (score <= 0) {
        return;
      }
      YDWERPGAddKey(p, "jf", score);
    }

    function AddPlayerUnitKills(player p) {
      // 默认不限制杀敌数
      integer playerId = GetPlayerId(p);
      integer maxKills = MAX_KILL_COUNT;
      // 判断当前难度，难度对应杀敌数积累上限
      if (udg_CurrDiff == 0) {
        // 普通杀敌上限3000；超过不计算杀敌
        maxKills = 3000;
      } else if (udg_CurrDiff == 1) {
        // 困难杀敌上限14400；超过不计算杀敌
        maxKills = 14400;
      }
      // 如果杀敌数超过累积上限，但是之前保存的没有超过，将之前保存的杀敌数修改为上限
      if (udg_UnitKills[playerId] > maxKills){
        if (PlayerUnitKills[playerId] > maxKills) {
          return;
        }
        PlayerUnitKills[playerId] = maxKills;
      } else {
        PlayerUnitKills[playerId] = udg_UnitKills[playerId];
      }
      YDWERPGSetKey(p, "dwsd", PlayerUnitKills[playerId]);
    }

    function GetPlayerTitle(integer playerId) -> string {
      if (PlayerTitleCurr[playerId] == 2) {
        return GeneralTitleStr[PlayerGeneralLevel[playerId]];
      }
      return PlayerTitleStr[PlayerTitleCurr[playerId]];
    }

    // 一骑当千
    function GivePlayerThousandKillTitle(player p) {
      integer playerId = GetPlayerId(p);
      if (havebinary(PlayerTitles[playerId], 0)) {
        return;
      }
      DisplayTimedTextToForce(udg_PlayerForce, 30.0, "玩家"+YDWEGetPlayerColorString(p, GetPlayerName(p))+"奋勇杀敌，获得称号"+PlayerTitleStr[1]+"！\n称号效果：英雄升级时额外获得可分配属性点。\n下次进入游戏时生效。" );
      SetPlayerTitle(p, 1);
    }

    // 将军
    function GivePlayerGeneralTitle(player p){
      if (PlayerGeneralLevel[GetPlayerId(p)] < 17 && udg_UnitKills[GetPlayerId(p)] >= GeneralUpgradeNeedKills[PlayerGeneralLevel[GetPlayerId(p)] + 1]) {
        PlayerGeneralLevel[GetPlayerId(p)] = PlayerGeneralLevel[GetPlayerId(p)] + 1;
        YDWERPGSetKey(p, "jj", PlayerGeneralLevel[GetPlayerId(p)]);
        AddPlayerUnitKills(p);
        DisplayTimedTextToForce(udg_PlayerForce, 30.0, "玩家"+YDWEGetPlayerColorString(p, GetPlayerName(p))+"率部杀敌，授予官职"+GeneralTitleStr[PlayerGeneralLevel[GetPlayerId(p)]]+"！所率将士获得士气提升！" );
        if (havebinary(PlayerTitles[GetPlayerId(p)], 1)) {
          return;
        }
        SetPlayerTitle(p, 2);
      }
    }

    // 建筑师
    function GivePlayerBuildMasterTitle(player p) {
      integer playerId = GetPlayerId(p);
      if (havebinary(PlayerTitles[playerId], 2)) {
        return;
      }
      DisplayTimedTextToForce(udg_PlayerForce, 30.0, "玩家"+YDWEGetPlayerColorString(p, GetPlayerName(p))+"建筑技巧娴熟，获得称号"+PlayerTitleStr[3]+"！\n称号效果：建筑建造、升级速度提升80%。\n下次进入游戏时生效。" );
      SetPlayerTitle(p, 3);
    }

    function PlatUpdateWinLose(integer winorlose) {
      integer i;
      string playernames = "";
      // winorlose 1 完胜 0 胜利 失败
      if (udg_CurrDiff <= 2 && winorlose == 1) {
        DisplayTimedTextToForce(udg_PlayerForce, 30.0, udg_DiffStr[udg_CurrDiff]+"难度通关，请继续挑战下一个难度。");
      }
      for (0 <= i < 8) {
        if (GetPlayerController(Player(i))==MAP_CONTROL_USER && GetPlayerSlotState(Player(i)) == PLAYER_SLOT_STATE_PLAYING) {
          if (winorlose == 1) {
            // 完胜
            YDWERPGSetKey(Player(i), "ws"+I2S(udg_CurrDiff+1), PlayerWinStreak[i]+winorlose);
            YDWERPGSetKey(Player(i), "wc"+I2S(udg_CurrDiff+1), PlayerWinCount[i]+winorlose);
            AddPlayerPlatScore(Player(i), 50*(udg_CurrDiff+1));
            if (udg_CurrDiff == 1) {
              // 如果是困难难度
              playernames = playernames + YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "，";
            } else if (udg_CurrDiff == 2 && udg_Kills[i] >= 1000) {
              // 如果是专家难度，杀敌大于1000
              playernames = playernames + YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "，";
            } else if (udg_CurrDiff == 3 && udg_Kills[i] >= 1000) {
              // 如果是史诗难度，杀敌大于1000
              playernames = playernames + YDWEGetPlayerColorString(Player(i), GetPlayerName(Player(i))) + "，";
            }
          } else {
            // 其他情况连胜清空
            YDWERPGSetKey(Player(i), "ws"+I2S(udg_CurrDiff+1), 0);
          }
        }
      }
      if (udg_CurrDiff == 1 && winorlose == 1) {
        DisplayTimedTextToForce(udg_PlayerForce, 30.0, playernames + "超越了自我。");
      } else if (udg_CurrDiff == 2 && winorlose == 1) {
        DisplayTimedTextToForce(udg_PlayerForce, 30.0, "玩家" + playernames + "贡献突出！");
      } else if (udg_CurrDiff == 3 && winorlose == 1) {
        DisplayTimedTextToForce(udg_PlayerForce, 30.0, playernames + "扫清六合，席卷八荒！");
        DisplayTimedTextToForce(udg_PlayerForce, 30.0, "恭喜你们，超越了自我，创造了奇迹！"+udg_DiffStr[udg_CurrDiff]+"难度通关！");
      }
    }

    function DistributePlayerResource(){
      if (udg_IsOnPlat11) {
        ForForce(udg_PlayerForce, function DistributePlayerResourceAction);
      }
    }
  }

  function onInit() {
    InitDialogs();
  }
  
}