library SummonPower requires HeroLearnAbility, UnitMaxState, BonusMod, BonusAdd {

  private constant integer BUFFID = 'BUan';

  function UnitSummoned() -> boolean {
    unit hero = GetSummoningUnit();
    unit summon = GetSummonedUnit();
    integer level = 0;
    integer abiLevel = 0;
    integer summonLevel = 0;
    integer unitId = 0;
    integer itemId = 0;
    real shili = 0;
    integer atk = 0;
    integer atkspd = 0;
    real hp = 0;
    real mp = 0;
    if (IsUnitType(hero, UNIT_TYPE_HERO) == false) {
      return false;
    }
    level = GetHeroLevel(hero);
    summonLevel = GetUnitLevel(summon);
    // 获取召唤技能的技能等级
    // 根据哈希表ht_abi存储的技能，对应物品数据，找到单位当前等级
    // 判断单位是否有操纵尸体BUFF，有则适用操纵尸体BUFF作为KEY
    if (GetUnitAbilityLevel(summon, BUFFID) > 0) {
      unitId = BUFFID;
    } else {
      unitId = GetUnitTypeId(summon);
    }
    itemId = LoadInteger(ht_abi, unitId, 'item');
    if (itemId > 0) {
      // 根据itemId获取英雄对应技能等级
      abiLevel = GetUnitAbilityLevelByItemId(hero, itemId);
    }
    // 每等级发挥1%实力
    // 每技能等级多发挥10%实力
    shili = level * 0.01 + abiLevel * 0.1;
    // 公式：
    // 攻击力 = 英雄智力 * 0.2 * 召唤物等级
    // 生命值 = 英雄力量 * 召唤物等级
    // 魔法值 = 英雄智力 * 召唤物等级
    // 攻击速度(%) = 英雄敏捷 / 召唤物等级
    atk = R2I(GetHeroInt(hero, false) * 0.2 * summonLevel * shili);
    atkspd = R2I(GetHeroAgi(hero, false) / summonLevel * shili);
    hp = GetHeroStr(hero, false) * summonLevel * shili;
    mp = GetHeroStr(hero, false) * summonLevel * shili;

    if (atk >= 1) {
      AddUnitAtk(summon, atk);
    }
    if (hp >= 1.) {
      AddUnitMaxState(summon, UNIT_STATE_MAX_LIFE, hp);
    }
    if (mp >= 1.) {
      AddUnitMaxState(summon, UNIT_STATE_MAX_MANA, mp);
    }
    if (atkspd >= 1) {
      SetUnitBonus(summon, BONUS_ATTACK_SPEED, atkspd);
    }
    SetUnitState(summon, UNIT_STATE_MANA, GetUnitState(summon, UNIT_STATE_MAX_MANA));

    return false;
  }

  function onInit() {
    trigger trg = CreateTrigger();
    integer i = 0;
    for (0<=i<10) {
      TriggerRegisterPlayerUnitEvent(trg, Player(i), EVENT_PLAYER_UNIT_SUMMON, null);
    }
    TriggerAddCondition(trg, function UnitSummoned);
  }
}